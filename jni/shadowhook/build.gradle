plugins {
    id 'com.android.library'
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    ndkVersion rootProject.ext.ndkVersion
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        consumerProguardFiles 'consumer-rules.pro'
        externalNativeBuild {
            cmake {
                abiFilters rootProject.ext.abiFilters.split(",")
                arguments "-DANDROID_STL=none"
                if(rootProject.ext.useASAN){
                    arguments "-DANDROID_ARM_MODE=arm"
                    arguments "-DUSEASAN=ON"
                }
            }
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version rootProject.ext.cmakeVersion
        }
    }
    compileOptions {
        sourceCompatibility rootProject.ext.javaVersion
        targetCompatibility rootProject.ext.javaVersion
    }
    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled false
        }
    }
    if (rootProject.ext.useASAN) {
        packagingOptions {
            doNotStrip "**/*.so"
        }
    }
    buildFeatures {
        prefabPublishing true
    }
    prefab {
        shadowhook {
            headers "src/main/cpp/include"
        }
    }
}

apply from: rootProject.file('gradle/check.gradle')
apply from: rootProject.file('gradle/publish.gradle')

if (rootProject.ext.dependencyOnLocalLibrary) {
    tasks.whenTaskAdded { task ->
        if (task.name == 'bundleReleaseLocalLintAar') {
            task.dependsOn('prefabReleaseConfigurePackage')
        }
    }
}
